'Email Makroer
Public Sub VagtAendringer()
    Dim NewMail As Outlook.MailItem

    'Change the template file folder path according to your case
    Set NewMail = Application.CreateItemFromTemplate("H:\Skabeloner\Vagtplan for sidste uge.oft")


    With NewMail
        theBody = .HTMLBody

        theBody = Replace(theBody, "{{uge}}", Format(Now, "ww"))
        theBody = Replace(theBody, "{{dato}}", Format(Now, "dd.mm.yyyy"))
        theBody = Replace(theBody, "{{dag}}", WeekdayName(Weekday(Now, vbMonday)))

        .HTMLBody = theBody
    End With

    NewMail.Display

    Set NewMail = Nothing
End Sub


'Kalender Makroer
Public Function templatePath(filename As String)
    templatePath = "H:\Skabeloner\" & filename & ".oft"
End Function

Public Sub RSR1(Optional startDate = "QJZ")
    CreateEventWithTime startDate, "07:00", 2, templatePath("Fri")
    CreateEventWithTime startDate, "09:00", 8, templatePath("På RSR")
    CreateEventWithTime startDate, "09:00", 0.5, templatePath("Opstartstid")
    CreateEventWithTime startDate, "13:00", 0.5, templatePath("Frokost")
    CreateEventWithTime startDate, "17:00", 6, templatePath("Fri")
End Sub

Public Sub RSH1(Optional startDate = "QJZ")
    CreateEventWithTime startDate, "07:00", 2, templatePath("Fri")
    CreateEventWithTime startDate, "09:00", 8, templatePath("På RSH")
    CreateEventWithTime startDate, "09:00", 0.5, templatePath("Opstartstid")
    CreateEventWithTime startDate, "13:00", 0.5, templatePath("Frokost")
    CreateEventWithTime startDate, "17:00", 6, templatePath("Fri")
End Sub

Public Sub AR1(Optional startDate = "QJZ")
    CreateEventWithTime startDate, "07:00", 8, templatePath("Fri")
    CreateEventWithTime startDate, "15:00", 8, templatePath("På RSH")
    CreateEventWithTime startDate, "18:30", 0.5, templatePath("Aftensmad")
    CreateEventWithTime startDate, "23:00", 6.5, templatePath("Vagt")
End Sub

Public Sub A1(Optional startDate = "QJZ")
    CreateEventWithTime startDate, "07:00", 7, templatePath("Fri")
    CreateEventWithTime startDate, "14:00", 8, templatePath("På RSH")
    CreateEventWithTime startDate, "18:30", 0.5, templatePath("Aftensmad")
    CreateEventWithTime startDate, "22:00", 1, templatePath("Fri")
End Sub

Public Sub A2(Optional startDate = "QJZ")
    CreateEventWithTime startDate, "07:00", 6, templatePath("Fri")
    CreateEventWithTime startDate, "13:00", 9, templatePath("På RSH")
    CreateEventWithTime startDate, "18:30", 0.5, templatePath("Aftensmad")
    CreateEventWithTime startDate, "22:00", 1, templatePath("Fri")
End Sub

Public Sub AR3(Optional startDate = "QJZ")
    CreateEventWithTime startDate, "07:00", 8, templatePath("Fri")
    CreateEventWithTime startDate, "15:00", 8, templatePath("På RSH")
    CreateEventWithTime startDate, "18:30", 0.5, templatePath("Aftensmad")
    CreateEventWithTime startDate, "23:00", 54.5, templatePath("Vagt")
End Sub

Public Sub Fri(Optional startDate = "QJZ")
    CreateEventWithTime startDate, "07:00", 16, templatePath("Fri")
End Sub

Public Sub EventRSR()
    CreateEvent templatePath("På RSR")
End Sub

Public Sub EventRSH()
    CreateEvent templatePath("På RSH")
End Sub

Public Sub EventWFH()
    CreateEvent templatePath("Arbejder Hjemmefra")
End Sub

Public Sub EventOffSite()
    CreateEvent templatePath("Ude af huset")
End Sub

Public Sub EventVagt()
    CreateEvent templatePath("Vagt")
End Sub

Public Sub EventFri()
    CreateEvent templatePath("Fri")
End Sub

Public Sub EventFrokost()
    CreateEvent templatePath("Frokost")
End Sub

Public Sub EventAftensmad()
    CreateEvent templatePath("Aftensmad")
End Sub

Public Sub EventOpstartstid()
    CreateEvent templatePath("Opstartstid")
End Sub


Private Sub CreateEvent(templatePath)
    On Error GoTo eh

    Dim datStart As Date
    Dim datEnd As Date
    Dim oView As Outlook.view
    Dim oCalView As Outlook.CalendarView
    Dim oExpl As Outlook.Explorer
    Dim oFolder As Outlook.Folder
    Const datNull As Date = #1/1/4501#

    Set newEvent = Application.CreateItemFromTemplate(templatePath)

    Set oExpl = Application.activeExplorer
    Set oFolder = Application.activeExplorer.CurrentFolder
    Set oView = oExpl.CurrentView

    If oView.ViewType = olCalendarView Then
        Set oCalView = oExpl.CurrentView

        datStart = oCalView.SelectedStartTime
        datEnd = oCalView.SelectedEndTime

        If datStart <> datNull And datEnd <> datNull Then
            newEvent.Start = datStart
            newEvent.End = datEnd
        End If
    End If

    'newEvent.Display
    newEvent.Save
    Set newEvent = Nothing
Done:
    Exit Sub
eh:
    MsgBox "Error " & Err.Number & ": " & Err.Description
End Sub


Private Sub CreateEventWithTime(startDate, startTime, duration, templatePath)
    On Error GoTo eh

    Dim datStart As Date
    Dim datEnd As Date
    Dim oView As Outlook.view
    Dim oCalView As Outlook.CalendarView
    Dim oExpl As Outlook.Explorer
    Dim oFolder As Outlook.Folder
    Const datNull As Date = #1/1/4501#

    Set newEvent = Application.CreateItemFromTemplate(templatePath)

    Set oExpl = Application.activeExplorer
    Set oFolder = Application.activeExplorer.CurrentFolder
    Set oView = oExpl.CurrentView

    If startDate = "QJZ" Then
        Set oCalView = oExpl.CurrentView
        startDate = DateValue(oCalView.SelectedStartTime)
    End If

    If oView.ViewType = olCalendarView Then
        datStart = startDate + TimeValue(startTime)
        datEnd = datStart + (duration / 24)

        If datStart <> datNull And datEnd <> datNull Then
            newEvent.Start = datStart
            newEvent.End = datEnd
        End If
    End If

    'newEvent.Display
    newEvent.Save
    Set newEvent = Nothing
Done:
    Exit Sub
eh:
    MsgBox "Error " & Err.Number & ": " & Err.Description
End Sub


Public Sub CreateEventsFromSelection()
    Dim myOlExp As Outlook.Explorer
    Dim oAppt As Outlook.AppointmentItem
    Dim oCalendarFolder As Outlook.Folder
    Dim calendarItems As Outlook.Items
    Dim filteredItems As Outlook.Items
    Dim apptSubject As String
    Dim apptStartDate As Date
    Dim startDate As Date
    Dim endDate As Date
    Dim filter As String
    Dim view As Outlook.view

    ' Set the active explorer and view
    Set myOlExp = Application.activeExplorer
    Set view = myOlExp.CurrentView

    ' Ensure the user is in a Calendar view
    If Not TypeOf view Is Outlook.CalendarView Then
        MsgBox "Please ensure you're viewing a calendar."
        Exit Sub
    End If

    ' Get the selected date range from the Calendar view
    With view
        startDate = .SelectedStartTime
        endDate = .SelectedEndTime
    End With

    If TimeValue(startDate) = "00:00:00" Then
        startDate = DateAdd("h", 0, startDate)
        startDate = DateAdd("n", 0, startDate)
        startDate = DateAdd("s", 0, startDate)
    End If

    If TimeValue(endDate) = "00:00:00" Then
        endDate = DateAdd("h", 23, endDate)
        endDate = DateAdd("n", 59, endDate)
        endDate = DateAdd("s", 59, endDate)
    End If

    ' Access the "Vagtplan" calendar (subfolder of your main Calendar)
    Set ns = Application.GetNamespace("MAPI")
    On Error Resume Next
    Set oCalendarFolder = ns.Session.DefaultStore.GetDefaultFolder(olFolderCalendar).Folders("Vagtplan")
    On Error GoTo 0

    If oCalendarFolder Is Nothing Then
        MsgBox "The 'Vagtplan' calendar could not be found."
        Exit Sub
    End If

    ' Get the items in the calendar and filter them by the selected date range
    Set calendarItems = oCalendarFolder.Items
    calendarItems.Sort "[Start]", False

    ' Create a filter to get items within the selected date range
    filter = "[Start] >= '" & Format(startDate, "DD-MM-YYYY HH:NN") & "' AND [End] <= '" & Format(endDate, "DD-MM-YYYY HH:NN") & "'"
    Set filteredItems = calendarItems.Restrict(filter)

    ' Loop through the filtered items
    For Each oAppt In filteredItems
        If oAppt.Class = OlObjectClass.olAppointment Then
            apptSubject = oAppt.Subject
            apptStartDate = DateValue(oAppt.Start)

            ' Check the subject and call appropriate subroutine based on matching pattern
            If apptSubject Like "RSR *" Then
                RSR1 apptStartDate
            ElseIf apptSubject Like "7-15 *" Then
                RSH1 apptStartDate
            ElseIf apptSubject Like "9-17 *" Then
                RSH1 apptStartDate
            ElseIf apptSubject Like "14-22 *" Then
                A1 apptStartDate
            ElseIf apptSubject Like "13-22 *" Then
                A2 apptStartDate
            ElseIf apptSubject Like "15-23R07 *" Then
                AR1 apptStartDate
            ElseIf apptSubject Like "15-23R24 *" Then
                AR3 apptStartDate
            ElseIf apptSubject Like "FRI *" Then
                Fri apptStartDate
            ElseIf apptSubject Like "Fri *" Then
                Fri apptStartDate
            ElseIf apptSubject Like "Afspadsering *" Then
                Fri apptStartDate
            ElseIf apptSubject Like "Ferie *" Then
                Fri apptStartDate
            End If
        End If
    Next oAppt

    MsgBox "Processing complete."
End Sub


Public Sub DeleteScheduleEvents()
    DeleteCalendarEventsByDescription "#ScheduleMacro"
End Sub


Public Sub DeleteCalendarEventsByDescription(searchString)
    Dim outlookApp As Outlook.Application
    Dim calendarFolder As Outlook.Folder
    Dim calendarItems As Outlook.Items
    Dim filteredItems As Outlook.Items
    Dim eventItem As Object
    Dim startDate As Date
    Dim endDate As Date
    Dim filter As String
    Dim activeExplorer As Outlook.Explorer
    Dim view As Outlook.view
    Dim i As Long

    ' Initialize Outlook application and get the active explorer
    Set outlookApp = Outlook.Application
    Set activeExplorer = outlookApp.activeExplorer

    ' Ensure the active explorer is the calendar view
    If activeExplorer.CurrentFolder.DefaultItemType <> olAppointmentItem Then
        MsgBox "Please select the Calendar view."
        Exit Sub
    End If

    ' Get the currently selected date range from the active calendar view
    Set view = activeExplorer.CurrentView
    If TypeOf view Is Outlook.CalendarView Then
        With view
            startDate = .SelectedStartTime
            endDate = .SelectedEndTime
        End With
    Else
        MsgBox "Please make sure you're in the calendar view."
        Exit Sub
    End If

    If TimeValue(startDate) = "00:00:00" Then
        startDate = DateAdd("h", 0, startDate)
        startDate = DateAdd("n", 0, startDate)
        startDate = DateAdd("s", 1, startDate)
    End If

    If TimeValue(endDate) = "00:00:00" Then
        endDate = DateAdd("h", 0, endDate)
        endDate = DateAdd("n", 0, endDate)
        endDate = DateAdd("s", 1, endDate)
    End If

    ' Get the Calendar folder
    Set calendarFolder = outlookApp.Session.GetDefaultFolder(olFolderCalendar)

    If calendarFolder Is Nothing Then
        MsgBox "The calendar could not be found."
        Exit Sub
    End If

    ' Create a filter for the date range
    filter = "[Start] >= '" & Format(startDate, "DD-MM-YYYY HH:NN") & "' AND [End] <= '" & Format(endDate, "DD-MM-YYYY HH:NN") & "'"

    ' Get calendar items within the date range
    Set calendarItems = calendarFolder.Items
    calendarItems.Sort "[Start]", False
    Set filteredItems = calendarItems.Restrict(filter)

    ' Loop through the filtered items in reverse order to safely delete items
    For i = filteredItems.Count To 1 Step -1
        Set eventItem = filteredItems.Item(i)
        ' Check if the event has a description and if it contains the search string
        If TypeOf eventItem Is Outlook.AppointmentItem Then
            If InStr(1, eventItem.Body, searchString, vbTextCompare) > 0 Then
                ' Delete the event if the string is found in the description
                eventItem.Delete
            End If
        End If
    Next i

    MsgBox "Events cleared"
End Sub

